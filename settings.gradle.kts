pluginManagement {
	repositories {
		mavenLocal()
		mavenCentral()
		gradlePluginPortal()
		maven("https://maven.fabricmc.net/") { name = "Fabric" }
		maven("https://maven.neoforged.net/releases/") { name = "NeoForged" }
		maven("https://maven.kikugie.dev/snapshots") { name = "KikuGie Snapshots" }
		maven("https://maven.kikugie.dev/releases") { name = "KikuGie Releases" }
		maven("https://maven.parchmentmc.org") { name = "ParchmentMC" }
//		maven("https://maven.terraformersmc.com/") { name = "TerraformersMC" }
		exclusiveContent {
			forRepository { maven("https://api.modrinth.com/maven") { name = "Modrinth" } }
			filter { includeGroup("maven.modrinth") }
		}
	}
	includeBuild("build-logic")
}

plugins {
	id("org.gradle.toolchains.foojay-resolver-convention") version "1.0.0"
	id("dev.kikugie.stonecutter") version "0.9.1-beta.2"
}

stonecutter {
	create(rootProject) {
		fun match(version: String, vararg loaders: String) =
			loaders.forEach {
				version("$version-$it", version).buildscript = if (version >= "26.1") {
					"build.$it" + "-unobf.gradle.kts"
				} else {
					"build.$it.gradle.kts"
				}
			}
		//match("1.21.1", "fabric", "neoforge") //TODO:add neoforge support
		match("1.21.1", "fabric")
		match("1.21.2", "fabric")
		match("1.21.5", "fabric")
		match("1.21.6", "fabric")
		match("1.21.10", "fabric")
		match("1.21.11", "fabric")
		match("26.1", "fabric")

		vcsVersion = "1.21.1-fabric"
	}
}
